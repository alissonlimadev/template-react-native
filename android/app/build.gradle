apply plugin: "com.android.application"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
apply from: project(':react-native-config').projectDir.getPath() + "/dotenv.gradle"

project.ext.envConfigFiles = [
    debug: ".env.dev",
    hmlDebug: ".env.hml",
    prdDebug: ".env.prd",
    releaseStaging: ".env.hml",
    release: ".env.prd",
]

import com.android.build.OutputFile

project.ext.react = [
    entryFile: "index.js",
    inputExcludes: ["ios/**", "__tests__/**", "bundle_out/**"],
    enableHermes: false,  // clean and rebuild if changing
]

apply from: "../../node_modules/react-native/react.gradle"
apply from: "../../node_modules/@sentry/react-native/sentry.gradle"
apply from: "../../node_modules/react-native-code-push/android/codepush.gradle"


def enableSeparateBuildPerCPUArchitecture = false

def enableProguardInReleaseBuilds = false

def jscFlavor = 'org.webkit:android-jsc:+'

def enableHermes = project.ext.react.get("enableHermes", false);

// def nativeArchitectures = project.getProperties().get("reactNativeDebugArchitectures")

android {
    ndkVersion rootProject.ext.ndkVersion
    compileSdkVersion rootProject.ext.compileSdkVersion

    lintOptions{
      disable 'MissingTranslation'
      disable 'MissingDefaultResource'
    }

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.template"
        resValue "string", "build_config_package", "com.template"
        resValue 'string', "CODE_PUSH_APK_BUILD_TIME", String.format("\"%d\"", System.currentTimeMillis())
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            if (project.hasProperty('MYAPP_UPLOAD_STORE_FILE')) {
                storeFile file(MYAPP_UPLOAD_STORE_FILE)
                storePassword MYAPP_UPLOAD_STORE_PASSWORD
                keyAlias MYAPP_UPLOAD_KEY_ALIAS
                keyPassword MYAPP_UPLOAD_KEY_PASSWORD
            }
        }
    }
    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            universalApk true
            include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            resValue "string", "CodePushDeploymentKey", '"DEV_NAO_PRECISA"'
            applicationIdSuffix ".dev"
        }
        hmlDebug {
            initWith debug
            matchingFallbacks = ['debug']
            applicationIdSuffix ".hml"
        }
        prdDebug {
            initWith debug
            matchingFallbacks = ['debug']
            applicationIdSuffix ".prd"
        }
        releaseStaging {
            initWith release
            matchingFallbacks = ['debug', 'release']
            signingConfig signingConfigs.release
            resValue "string", "CodePushDeploymentKey", '"gZxeZUgRCZLfsu0ZgtEalLDBM2jeqEZRb6QiL"'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            resValue "string", "CodePushDeploymentKey", '"VFEbyw9HDU5aLQXjoACWSVAE5_dMiZ3_atLvp"'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def versionCodes = ["armeabi-v7a": 1, "x86": 2, "arm64-v8a": 3, "x86_64": 4]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        defaultConfig.versionCode * 1000 + versionCodes.get(abi)
            }

        }
    }

    gradle.taskGraph.beforeTask { Task task ->
      if (task.name ==~ /process.*GoogleServices/) {
          android.applicationVariants.all { variant ->
              if (task.name ==~ /(?i)process${variant.name}GoogleServices/) {
                  if(variant.buildType.name == 'release'){
                    println "Release - copyng google-service.json to src/"
                    copy {
                        from "/src/release"
                        into '.'
                        include 'google-services.json'
                    }
                  } else if (variant.buildType.name == 'debug') {
                    println "Debug - copyng google-service.json to src/"
                    copy {
                        from "/src/debug"
                        into '.'
                        include 'google-services.json'
                    }
                  } else if (variant.buildType.name == 'hmlDebug') {
                    println "HmlDebug - copyng google-service.json to src/"
                    copy {
                        from "/src/hmlDebug"
                        into '.'
                        include 'google-services.json'
                    }
                  } else if (variant.buildType.name == 'prdDebug') {
                    println "PrdDebug - copyng google-service.json to src/"
                    copy {
                        from "/src/prdDebug"
                        into '.'
                        include 'google-services.json'
                    }
                  } else {
                    println "Releasestaging - copyng google-service.json to src/"
                    copy {
                        from "/src/releaseStaging"
                        into '.'
                        include 'google-services.json'
                    }
                  }
              }
          }
      }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"  // From node_modules

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"

    debugImplementation("com.facebook.flipper:flipper:${FLIPPER_VERSION}") {
        exclude group:'com.facebook.fbjni'
    }

    debugImplementation("com.facebook.flipper:flipper-network-plugin:${FLIPPER_VERSION}") {
        exclude group:'com.facebook.flipper'
        exclude group:'com.squareup.okhttp3', module:'okhttp'
    }

    debugImplementation("com.facebook.flipper:flipper-fresco-plugin:${FLIPPER_VERSION}") {
        exclude group:'com.facebook.flipper'
    }

    if (enableHermes) {
        def hermesPath = "../../node_modules/hermes-engine/android/";
        debugImplementation files(hermesPath + "hermes-debug.aar")
        releaseImplementation files(hermesPath + "hermes-release.aar")
    } else {
        implementation jscFlavor
    }
}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
    from configurations.implementation
    into 'libs'
}

apply from: file("../../node_modules/@react-native-community/cli-platform-android/native_modules.gradle"); applyNativeModulesAppBuildGradle(project)
